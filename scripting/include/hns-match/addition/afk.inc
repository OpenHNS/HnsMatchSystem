#if defined _afk_included
  #endinput
#endif

#define _afk_included

enum _:AfkData_s {
	bool:is_afk,
	afk_timer
};
new eAfkData[MAX_PLAYERS + 1][AfkData_s];
new iPlayersAfk;

new Float:flAfkOrigin[MAX_PLAYERS + 1][3];

public taskCheckAfk() {
	if (g_iCurrentMode != e_mMatch) {
		ResetAfkData();
		return;
	}

	new iPlayers[MAX_PLAYERS], iNum;
	get_players(iPlayers, iNum, "ach");

	for(new i; i < iNum; i++) {
		new id = iPlayers[i];

		if (PlayerIsAfk(id)) {
			eAfkData[id][is_afk] = true;
			iPlayersAfk++;
		}
	}

	if (iPlayersAfk) {
		cmdStartPause(0);
		chat_print(0, "%L", 0, "AFK_PAUSE", iPlayersAfk);
		set_task(1.0, "taskAfk", .flags = "b");
	}
}

public taskAfk() {
	if (g_iCurrentMode != e_mPaused) {
		ResetAfkData();
		return;
	}

	new iPlayers[MAX_PLAYERS], iNum, szBuffer[512];
	get_players(iPlayers, iNum, "h");

	add(szBuffer, charsmax(szBuffer), "AFK Players [wait time]:^n");

	for(new i; i < iNum; i++) {
		new id = iPlayers[i];
		new szTime[16];

		if (eAfkData[id][is_afk]) {
			if (getUserTeam(id) == TEAM_SPECTATOR || !is_user_alive(id)) {
				arrayset(eAfkData[id], 0, AfkData_s);
				arrayset(flAfkOrigin[id], 0.0, sizeof(flAfkOrigin[]));
				iPlayersAfk--;
				continue;
			}

			if (!PlayerIsAfk(id)) {
				arrayset(eAfkData[id], 0, AfkData_s);
				arrayset(flAfkOrigin[id], 0.0, sizeof(flAfkOrigin[]));
				iPlayersAfk--;
			} else {
				eAfkData[id][afk_timer]++;
				fnConvertTime(eAfkData[id][afk_timer] * 1.0, szTime, 23, false);
				add(szBuffer, charsmax(szBuffer), fmt("%n (%s)^n", id, szTime));
			}
		}
	}

	if (!iPlayersAfk) {
		cmdStopPause(0);
		chat_print(0, "%L", 0, "AFK_UNPAUSE");
	} else {
		set_hudmessage(.red = 100, .green = 100, .blue = 100, .x = 0.15, .y = 0.20, .holdtime = 1.0);
		ShowSyncHudMsg(0, g_MsgSync, "%s", szBuffer);
	}
}

ResetAfkData() {
	remove_task();
	iPlayersAfk = 0;

	new iPlayers[MAX_PLAYERS], iNum;
	get_players(iPlayers, iNum, "h");

	for(new i; i < iNum; i++) {
		new id = iPlayers[i];
		arrayset(eAfkData[id], 0, AfkData_s);
		arrayset(flAfkOrigin[id], 0.0, sizeof(flAfkOrigin[]));
	}
}

stock bool:PlayerIsAfk(id) {
	new Float:origin[3]; get_entvar(id, var_origin, origin);

	if (get_distance_f(flAfkOrigin[id], origin) <= 1.0)
		return true;

	return false;
}

public taskSaveAfk() {
	new iPlayers[MAX_PLAYERS], iNum;
	get_players(iPlayers, iNum, "ach");

	for(new i; i < iNum; i++) {
		new id = iPlayers[i];
		get_entvar(id, var_origin, flAfkOrigin[id]);
	}
}
